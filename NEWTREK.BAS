10 REM SUPER STARTREK - MAY 16, 1978 - REQUIRES 24K MEMORY
30 REM
40 REM ****        **** STAR TREK ****         ****
50 REM **** SIMULATION OF A MISSION OF THE STARSHIP ENTERPRISE,
60 REM **** AS SEEN ON THE STAR TREK TV SHOW.
70 REM **** ORIGIONAL PROGRAM BY MIKE MAYFIELD, MODIGIED VERSION
80 REM **** PUBLISHED IN DEC'S "101 BASIC GAMES", BY DAVE AHL.
90 REM **** MODIFICATIONS TO THE LATTER (PLUS DEBUGGING) BY BOB
100 REM *** LEEDOM - APRIL & DECEMBER 1974,
110 REM *** WITH A LITTLE HELP FROM HIS FRIENDS . . .
120 REM *** COMMENTS, EPITHETS, AND SUGGESTIONS SOLICITED --
130 REM *** SEND TO:  R. C. LEEDOM
140 REM ***           WESTINGHOUSE DEFENSE & ELECTRONICS SYSTEMS CNTR.
150 REM ***           BOX 746, M.S. 338
160 REM ***           BALTIMORE, MD 21203
170 REM ***
180 REM *** CONVERTED TO MICROSOFT 8 K BASIC 3/16/78 BY JOHN BORDERS
190 REM *** LINE NUMBERS FROM VERSION STREK7 OF 1/12/75 PRESERVED AS
200 REM *** MUCH AS POSSIBLE WHILE USING MULTIPLE STATEMENTS PER LINE
205 REM *** SOME LINES ARE LONGER THAN 72 CHARACTERS; THIS WAS DONE
210 REM *** BY USING "?" INSTEAD OF "PRINT" WHEN ENTERING LINES.
215 REM ***
220 CLS : DIM A%(2500)
221 OPEN "PRISE.DAT" FOR INPUT AS #1
222 FOR I = 0 TO 2500
223    INPUT #1, A%(I)
224    NEXT I
225 CLOSE #1
226 'PUT (225,30),A%
230 PRINT"                    THE USS ENTERPRISE --- NCC-1701"
235 PRINT : PRINT : PRINT : PRINT : PRINT : PRINT
240 TIME=VAL(LEFT$(TIME$,2))*3600+VAL(MID$(TIME$,4,2))*60+VAL(RIGHT$(TIME$,2))
250 RANDOMIZE (TIME*.75 - 32400)
270 Z$="                         "
330 DIM G(8,8),C(10,2),K(3,3),N(3),Z(8,8),D(8)
370 T=INT(RND(1)*20+20)*100
372 T0=T
374 T9=35+INT(RND(1)*10)
376 D0=0
378 E=3000
380 E0=E
440 P=10
442 P0=P
444 S9=200
446 S=0
448 B9=0
450 K9=0
452 X$="" : CLR$ = "                                                 "
454 X0$=" IS "
460 ' Define some colors
461 
470 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
475 DEF FNR(R)=INT(RND(1)*7.98+1.01)
479 '   **********************************************
480 '   *****  INITIALIZE ENTERPRIZE'S POSITION  *****
481 '   **********************************************
490 Q1 = FNR(1)
492 Q2 = FNR(1)
494 S1 = FNR(1)
496 S2 = FNR(1)
530 FOR I = 1 TO 9
532   C(I,1) = 0
534   C(I,2) = 0
536 NEXT I
540 C(3,1) = -1
542 C(2,1) = -1
544 C(4,1) = -1
546 C(4,2) = -1
548 C(5,2) = -1
550 C(6,2) = -1
600 C(1,2) = 1
602 C(2,2) = 1
604 C(6,1) = 1
606 C(7,1) = 1
608 C(8,1) = 1
610 C(8,2) = 1
612 C(9,2) = 1
670 FOR I = 1 TO 8
672   D(I) = 0
674 NEXT I
710 A1$ = "NAVSRSLRSPHATORSHEDAMCOMXXX"
810 REM ---- SETUP WHAT EXHISTS IN THE GALAXY ----
815 REM ---- K3= # KLINGONS  B3= # STARBASES  S3 = # STARS ----
820 FOR I = 1 TO 8
822   FOR J = 1 TO 8
824     K3 = 0
826     Z(I,J)=0
828     R1 = RND(1)
850     IF R1 > .98 THEN K3=3: K9=K9+3: GOTO 980
860     IF R1 > .95 THEN K3=2: K9=K9+2: GOTO 980
870     IF R1 > .8 THEN K3=1: K9=K9+1
980     B3=0
982     IF RND(1) >  .96 THEN B3=1: B9=B9+1
1040    G(I,J) = K3*100 + B3*10 + FNR(1)
1042  NEXT J
1044 NEXT I
1046 IF K9 > T9 THEN T9 = K9+1
1100 IF B9 <> 0 THEN 1200
1150 IF G(Q1,Q2) < 200 THEN G(Q1,Q2)=G(Q1,Q2)+100: K9=K9+1
1160 B9 = 1
1162 G(Q1,Q2) = G(Q1,Q2)+10
1164 Q1=FNR(1)
1166 Q2=FNR(1)
1200 K7=K9
1202 IF B9 <> 1 THEN X$="S":X0$=" ARE "
1230 PRINT "YOUR ORDERS ARE AS FOLLOWS:"
1235 PRINT
1240 PRINT "   DESTROY THE";K9;"KLINGON WARSHIPS WHICH HAVE INVADED"
1250 PRINT "   THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
1260 PRINT "   ON STARDATE";T0+T9;"  THIS GIVES YOU";T9;"DAYS.  THERE";X0$
1270 PRINT "  ";B9;"STARBASE";X$;" IN THE GALAXY FOR RESUPPLYING YOUR SHIP"
1280 PRINT : PRINT "HIT ANY KEY WHEN READY TO ACCEPT COMMAND"
1290 IF INKEY$ = "" THEN 1290
1300 CLS
1310 REM ---- NEW QUADRANT ENTERED ----
1320 Z4=Q1
1322 Z5=Q2
1324 K3=0
1326 B3=0
1328 S3=0
1330 G5=0
1332 D4=.5*RND(1)
1334 Z(Q1,Q2)=G(Q1,Q2)
1430 GOSUB 9030
1432 PRINT:
1434 IF T0<> T THEN 1490
1460 PRINT "YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
1470 PRINT "IN THE GALGALACTIC QUADRANT, '";G2$;"'."
1472 GOTO 1500
1490 PRINT "NOW ENTERING ";G2$;" QUADRANT . . ."
1500 PRINT
1502 K3=INT(G(Q1,Q2)*.01)
1504 B3=INT(G(Q1,Q2)*.1)-10*K3
1540 S3=G(Q1,Q2)-100*K3-10*B3
1550 IF K3=0 THEN 1590
1560 PRINT "COMBAT AREA      CONDITION RED"
1570 IF S>200 THEN 1590
1580 PRINT "   SHIELDS DANGEROUSLY LOW"
1590 FOR I=1 TO 3
1592   K(I,1)=0
1594   K(I,2)=0
1596 NEXT I
1600 FOR I=1 TO 3
1610   K(I,3)=0
1620 NEXT I
1630 Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
1660 REM ---- POSITION ENTERPRISE IN QUADRANT, THEN PLACE "K3" KLINGONS, &
1670 REM      "B3" STARBASES, & "S3" STARS ELSEWHERE.
1680 A$= "<*>"
1690 Z1=S1
1700 Z2=S2
1710 GOSUB 8670
1712 IF K3<1 THEN 1820
1720 FOR I=1 TO K3
1730   GOSUB 8590
1740   A$="+K+"
1750   Z1=R1
1760   Z2=R2
1780   GOSUB 8670
1790   K(I,1)=R1
1792   K(I,2)=R2
1794   K(I,3)=S9*(.5+RND(1))
1800 NEXT I
1820 IF B3<1 THEN 1910
1880 GOSUB 8590
1890 A$= ">!<"
1892 Z1=R1
1894 B4=R1
1896 Z2=R2
1898 B5=R2
1900 GOSUB 8670
1910 FOR I=1 TO S3
1920   GOSUB 8590
1930   A$=" * "
1940   Z1=R1
1950   Z2=R2
1960   GOSUB 8670
1970 NEXT I
1980 GOSUB 6430
1990 IF S+E > 10 THEN IF (E > 10) OR (D(7) = 0) THEN 2060
2020 PRINT:PRINT "** FATAL ERROR **   YOU'VE JUST STRANDED YOUR SHIP IN "
2030 PRINT "SPACE"
2032 PRINT "YOU HAVE INSUFFICIENT MANEUVERING ENERGY,";
2040 PRINT " AND SHIELD CONTROL"
2042 PRINT "IS PRESENTLY INCAPABLE OF CROSS"
2050 PRINT "-CIRCUITING TO ENGINE ROOM!!"
2052 GOSUB 6220
2060 LOCATE 12,1 : INPUT "COMMAND";A$
2070 FOR IND = 1 TO 11 : PRINT CLR$ : NEXT IND : PRINT CLR$; : LOCATE 13,1
2080 FOR I=1 TO 9
2090   IF LEFT$(A$,3) <> MID$(A1$,3*I-2,3) THEN 2160
2140   ON I GOTO 2300, 1980, 4000, 4260, 4700, 5530, 5690, 7290, 6270
2160 NEXT I
2162 PRINT "ENTER ONE OF THE FOLLOWING:"
2180 PRINT "  NAV (TO SET COURSE)"
2190 PRINT "  SRS (FOR SHORT RANGE SENSOR SCAN)"
2200 PRINT "  LRS (FOR LONG RANGE SENSOR SCAN)"
2210 PRINT "  PHA (TO FIRE PHASERS)"
2220 PRINT "  TOR (TO FIRE PHOTON TORPEDOES)"
2230 PRINT "  SHE (TO RAISE OR LOWER SHIELDS)"
2240 PRINT "  DAM (FOR DAMAGE CONTROL REPORTS)"
2250 PRINT "  COM (TO CALL ON LIBRARY-COMPUTER)"
2260 PRINT "  XXX (TO RESIGN YOUR COMMAND)"
2262 PRINT
2270 GOTO 1990
2290 REM ---- COURSE CONTROL BEGINS HERE ----
2300 INPUT "COURSE (0-9)";C1
2302 IF C1=9 THEN C1=1
2310 IF C1>=1 AND C1<9 THEN 2350
2330 PRINT "   LT. SULU REPORTS, 'INCORRECT COURSE DATA, CAPTAIN!"
2340 GOTO 1990
2350 X$="8"
2352 IF D(1)<0 THEN X$="0.2"
2360 PRINT "WARP FACTOR (0-";X$;")";
2362 INPUT W1
2364 IF D(1)<0 AND W1>.2 THEN 2470
2380 IF W1>0 AND W1<=8 THEN 2490
2390 IF W1=0 THEN 1990
2420 PRINT "   CHIEF ENGINEER SCOTT REPORTS 'THE ENGINES WON'T TAKE WARP ";
2430 PRINT W1;"!"
2440 GOTO 1990
2470 PRINT "WARP ENGINES ARE DAMAGED.  MAXIMUM SPEED = WARP 0.2"
2480 GOTO 1990
2490 N = INT(W1*8+.5)
2492 IF E-N >= 0 THEN 2590
2500 PRINT "ENGINEERING REPORTS"
2505 PRINT "'INSUFFICIENT ENERGY AVAILABLE"
2510 PRINT "FOR MANEUVERING AT WARP";W1;"!'"
2530 IF S<N-E OR D(7)<0 THEN 1990
2550 PRINT "DEFLECTOR CONTROL ROOM ACKNOWLEGES"
2555 PRINT S;"UNITS OF ENERGY"
2560 PRINT "PRESENTLY DEPLOYED TO SHIELDS."
2570 GOTO 1990
2580 REM ***** KLINGONS MOVE/FIRE ON MOVING STARSHIP *****
2590 FOR I=1 TO K3
2600   IF K(I,3)=0 THEN 2700
2610   A$ = "   "
2620   Z1 = K(I,1)
2630   Z2 = K(I,2)
2640   GOSUB 8670
2650   GOSUB 8590
2660   K(I,1) = Z1
2670   K(I,2) = Z2
2680   A$ = "+K+"
2690   GOSUB 8670
2700 NEXT I
2710 GOSUB 6000
2720 D1=0
2730 D6=W1
2740 IF W1>=1 THEN D6=1
2770 FOR I=1 TO 8
2780   IF D(I)>=0 THEN 2880
2790   D(I) = D(I)+D6
2792   IF (D(I) > -.1) AND (D(I) < 0) THEN D(I)= -.1: GOTO 2880
2800   IF D(I)<0 THEN 2880
2810   IF D1 <> 1 THEN D1=1: PRINT "DAMAGE CONTROL REPORT:  ";
2840   PRINT TAB(8);
2850   R1=I
2860   GOSUB 8790
2870   PRINT G2$;" REPAIR COMPLETED."
2880 NEXT I
2890 IF RND(1) > .2 THEN 3070
2910 R1 = FNR(1)
2920 IF RMD(1) >= .6 THEN 3000
2930 D(R1) = D(R1)-(RND(1)*5+1)
2940 PRINT "DAMAGE CONTROL REPORT:  ";
2960 GOSUB 8790
2970 PRINT G2$;" DAMAGED"
2980 PRINT
2990 GOTO 3070
3000 D(R1) = D(R1)+RND(1)*3+1
3010 PRINT "DAMAGE CONTROL REPORT:  ";
3030 GOSUB 8790
3040 PRINT G2$; " STATE OF REPAIR IMPROVED": PRINT
3060 REM ***** BEGIN MOVING STARSHIP *****
3070 A$= "   "
3080 Z1 = INT(S1)
3090 Z2 = INT(S2)
3100 GOSUB 8670
3110 X1 = C(C1-.5,1) + (C(C1+.5,1)-C(C1-.5,1)) * (C1-INT(C1))
3120 X=S1
3130 Y=S2
3140 X2 = C(C1-.5,2) + (C(C1+.5,2)-C(C1-.5,2)) * (C1-INT(C1))
3150 Q4=Q1
3160 Q5=Q2
3170 FOR I=1 TO N
3180   S1 = S1+X1
3190   S2=S2+X2
3200   IF S1<1 OR S1>=9 OR S2<1 OR S2>=9 THEN 3500
3240   S8 = INT(S1)*24 + INT(S2)*3 - 26
3250   IF MID$(Q$,S8,2) = "  " THEN 3360
3320   S1 = INT(S1-X1)
3330   S2 = INT(S2-X2)
3340   PRINT "WARP ENGINES SHUT DOWN AT ";
3350   PRINT "SECTOR";S1;",";S2;"DUE TO BAD NAVAGATION"
3352   GOTO 3370
3360 NEXT I
3362 S1 = INT(S1)
3364 S2 = INT(S2)
3370 A$ = "<*>"
3380 Z1 = INT(S1)
3390 Z2 = INT(S2)
3400 GOSUB 8670
3410 GOSUB 3910
3420 T8=1
3430 IF W1<1 THEN T8= .1*INT(10*W1)
3450 T=T+T8
3460 IF T > T0+T9 THEN 6220
3470 REM ***** SEE IF DOCKED, THEN GET COMMAND *****
3480 GOTO 1980
3490 REM ***** EXCEEDED QUADRANT LIMITS *****
3500 X = 8*Q1 + X + N*X1
3510 Y = 8*Q2 + Y + N*X2
3520 Q1 = INT(X/8)
3530 Q2 = INT(Y/8)
3540 S1 = INT(X-Q1*8)
3550 S2 = INT(Y-Q2*8)
3560 IF S1=0 THEN Q1=Q1-1: S1=8
3590 IF S2=0 THEN Q2=Q2-1: S2=8
3620 X5=0
3630 IF Q1<1 THEN X5=1: Q1=1: S1=1
3670 IF Q1>8 THEN X5=1: Q1=8: S1=1
3710 IF Q2<1 THEN X5=1: Q2=1: S2=1
3750 IF Q2>8 THEN X5=1: Q2=8: S2=8
3790 IF X5=0 THEN 3860
3800 PRINT "LT. UHURA REPORTS MESSAGE FROM STARFLEET COMMAND:"
3810 PRINT "  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
3820 PRINT "  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
3830 PRINT "CHIEF ENGINEER SCOTT REPORTS  'WARP ENGINES SHUT DOWN"
3840 PRINT "  AT SECTOR";S1;",";S2;"OF QUADRANT";Q1;",";Q2;".'"
3850 IF T>T0+T9 THEN 6220
3860 IF 8*Q1+Q2=8*Q4+Q5 THEN 3370
3870 T=T+1
3880 GOSUB 3910
3890 GOTO 1320
3899 REM
3900 REM ***** MANEUVER ENERGY S/R *****
3901 REM
3910 E=E-N-10
3920 IF E>=0 THEN RETURN
3930 PRINT "SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
3940 S=S+E
3950 E=0
3960 IF S<=0 THEN S=0
3980 RETURN
3989 REM
3990 REM ***** LONG RANGE SENSOR SCAN CODE *****
3991 REM
4000 IF D(3)<0 THEN PRINT "LONG RANGE SENSORS ARE INOPERABLE": GOTO 1990
4010 LOCATE 12,40
4030 PRINT "LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
4040 O1$ = "-------------------"
4050 LOCATE 13,50 : PRINT O1$
4060 FOR I = Q1-1 TO Q1+1
4070   N(1)=-1
4080   N(2)=-2
4090   N(3)=-3
4100   FOR J = Q2-1 TO Q2+1
4120     IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-Q2+2) = G(I,J): Z(I,J)=G(I,J)
4180   NEXT J
4185    LOCATE 16 + (I-Q1)*2, 50
4190   FOR L = 1 TO 3
4200     PRINT ": ";
4202     IF N(L) < 0 THEN PRINT "*** ";: GOTO 4230
4210     QD$ = RIGHT$(STR$(N(L) + 1000), 3)
4212     IF LEFT$(QD$,1) <> "0" THEN COLOR 2
4214     PRINT LEFT$(QD$,1); : COLOR 7
4216     IF MID$(QD$,2,1) <> "0" THEN COLOR 4
4218     PRINT MID$(QD$,2,1); : COLOR 7
4220     PRINT RIGHT$(QD$,1);" ";
4230   NEXT L
4240   PRINT ":"
4242   LOCATE 17 + (I-Q1)*2, 50 : PRINT O1$
4244 NEXT I
4246 GOTO 1990
4249 REM
4250 REM ***** PHASER CONTROL CODE BEGINS HERE *****
4251 REM
4260 IF D(4)<0 THEN PRINT "PHASERS INOPERATIVE": GOTO 1990
4265 IF K3>0 THEN 4330
4270 PRINT "SCIENCE OFFICER SPOCK REPORTS  'SENSORS SHOW NO ENEMY SHIPS"
4280 PRINT "                                IN THIS QUADRANT'"
4290 GOTO 1990
4330 IF D(8)<0 THEN PRINT "COMPUTER FAILURE HAMPERS ACCURACY"
4350 PRINT "PHASERS LOCKED ON TARGET;  "
4360 PRINT "ENERGY AVAILABLE =";E;"UNITS"
4370 INPUT "NUMBER OF UNITS TO FIRE";X
4380 IF X<=0 THEN 1990
4400 IF E-X < 0 THEN 4360
4410 E=E-X
4420 IF D(7)<0 THEN X=X*RND(1)
4450 H1 = INT(X/K3)
4460 FOR KN = 1 TO 3
4470   IF K(KN,3) <= 0  THEN 4670
4475   I = KN
4480   H = INT((H1/FND(0))*(RND(1)+2))
4490   IF H > .15*K(KN,3) THEN 4530
4500   PRINT "SENSORS SHOW NO DAMAGE TO ENEMY AT ";K(KN,1);",";K(KN,2)
4510   GOTO 4670
4530   K(KN,3) = K(KN,3)-H
4540   PRINT H;"UNIT HIT ON KLINGON AT SECTOR";K(KN,1);",";K(KN,2)
4550   IF K(KN,3) <= 0 THEN PRINT "*** KLINGON DESTROYED ***": GOTO 4580
4560   REM PRINT "   (SENSORS SHOW";K(KN,3);"UNITS REMAINING)"
4570   GOTO 4670
4580   K3 = K3-1
4590   K9 = K9-1
4600   Z1 = K(KN,1)
4610   Z2 = K(KN,2)
4620   A$ = "   "
4630   GOSUB 8670
4650   K(KN,3) = 0
4660   G(Q1,Q2) = G(Q1,Q2)-100
4662   Z(Q1,Q2) = G(Q1,Q2)
4663   IF D(2) >= 0 THEN GOSUB 6420
4664   IF K9 <= 0 THEN 6370
4670 NEXT KN
4680 GOSUB 6000
4682 GOTO 1990
4689 REM
4690 REM ***** PHOTON TORPEDO CODE BEGINS HERE *****
4691 REM
4700 IF P <=0 THEN PRINT "ALL PHOTON TORPEDOES EXPENDED": GOTO 1990
4730 IF D(5)<0 THEN PRINT "PHOTON TUBES ARE NOT OPERATIONAL": GOTO 1990
4760 INPUT "PHOTON TORPEDO COURSE (1-9)";C1
4770 IF C1=9 THEN C1=1
4780 IF C1>=1 AND C1<9 THEN 4850
4790 PRINT "ENSIGN CHEKOV REPORTS," : PRINT "'INCORRECT COURSE DATA, SIR!'"
4800 GOTO 1990
4850 X1 = C(C1-.5,1)+(C(C1+.5,1)-C(C1-.5,1))*(C1-INT(C1))
4860 X2 = C(C1-.5,2)+(C(C1+.5,2)-C(C1-.5,2))*(C1-INT(C1))
4870 E = E-2
4880 P = P-1
4890 X = S1
4900 Y = S2
4910 PRINT "TORPEDO TRACK:"
4920 X = X+X1
4930 Y = Y+X2
4940 X3 = INT(X+.5)
4950 Y3 = INT(Y+.5)
4960 IF (X3<1) OR (X3>8) OR (Y3<1) OR (Y3>8) THEN 5490
5000 PRINT "               ";X3;",";Y3
5010 A$ = "   "
5020 Z1=X
5030 Z2=Y
5040 GOSUB 8830
5050 IF Z3<>0 THEN 4920
5060 A$ = "+K+"
5070 Z1=X
5080 Z2=Y
5090 GOSUB 8830
5100 IF Z3=0 THEN 5210
5110 PRINT "*** KLINGON DESTROYED ***"
5120 K3 = K3-1
5130 K9 = K9-1
5140 IF K9<=0 THEN 6370
5150 FOR I=1 TO 3
5160   IF (X3=K(I,1)) AND (Y3=K(I,2)) THEN 5190
5180 NEXT I
5182 I=3
5190 K(I,3)=0
5200 GOTO 5430
5210 A$=" * "
5220 Z1=X
5230 Z2=Y
5240 GOSUB 8830
5250 IF Z3=0 THEN 5280
5260 PRINT "STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY."
5270 GOSUB 6000
5272 GOTO 1990
5280 A$ = ">!<"
5290 Z1=X
5300 Z2=Y
5310 GOSUB 8830
5320 IF Z3=0 THEN 4760
5330 PRINT "*** STARBASE DESTROYED ***"
5340 B3=B3-1
5350 B9=B9-1
5360 IF (B9>0) OR (K9>T-T0-T9) THEN 5400
5370 PRINT "THAT DOES IT, CAPTAIN!!": PRINT "YOU ARE HEREBY RELIEVED OF COMMAND"
5380 PRINT "AND SENTENCED TO HARD LABOR ON CYGNUS 12!!"
5390 GOTO 6270
5400 PRINT "STARFLEET COMMAND REVIEWING YOUR RECORD"
5410 PRINT "TO CONSIDER COURT MARTIAL!"
5420 D0=0
5430 Z1=X
5440 Z2=Y
5450 A$="   "
5460 GOSUB 8670
5470 G(Q1,Q2) = K3*100 + B3*10 + S3
5480 Z(Q1,Q2) = GT(Q1,Q2)
5482 GOSUB 6000
5484 IF D(2) >= 0 THEN GOSUB 6420
5488 GOTO 1990
5490 PRINT "TORPEDO MISSED"
5500 GOSUB 6000
5510 GOTO 1990
5519 REM
5520 REM ***** SHIELD CONTROL *****
5521 REM
5530 IF D(7)<0 THEN PRINT "SHIELD CONTROL INOPERABLE": GOTO 1990
5560 PRINT "ENERGY AVAILABLE =";E+S
5570 INPUT "NUMBER OF UNITS TO SHIELDS"; X
5580 IF (X<0) OR (S=X) THEN PRINT "<SHIELDS UNCHANGED>": GOTO 1990
5590 IF X <= E+S THEN 5630
5600 PRINT "SHIELD CONTROL REPORTS"
5605 PRINT "'THIS IS NOT THE FEDERATION TREASURY.'"
5610 PRINT "<SHIELDS UNCHANGED>"
5620 GOTO 1990
5630 E = E+S-X
5640 S=X
5650 PRINT"DEFLECTOR CONTROL ROOM REPORT:"
5660 PRINT "  'SHIELDS NOW AT";INT(S);"UNITS PER YOUR COMMAND.'"
5670 GOTO 1990
5679 '
5680 '   ***** DAMAGE CONTROL *****
5681 '
5690 IF D(6) >= 0 THEN 5910
5700 PRINT "DAMAGE CONTROL REPORT NOT AVAILABLE"
5710 IF D0=0 THEN 1990
5720 D3=0
5730 FOR I = 1 TO 8
5740   IF D(I) < 0 THEN D3=D3+.1
5760 NEXT I
5770 IF D3=0 THEN 1990
5780 PRINT
5790 D3 = D3+D4
5800 IF D3>=1 THEN D3=.9
5810 PRINT "TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP;"
5820 PRINT "ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES"
5840 INPUT "WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)"; A$
5860 IF (A$<>"Y") AND (A$<>"y") THEN 1990
5870 FOR I=1 TO 8
5880   IF D(I)<0 THEN D(I)=0
5890 NEXT I
5900 T = T+D3+.1
5910 PRINT:PRINT "DEVICE             STATE OF REPAIR"
5912 FOR R1=1 TO 8
5920   GOSUB 8790
5930   PRINT G2$; LEFT$(Z$,25-LEN(G2$)); INT(D(R1)*100)*.01
5950 NEXT R1
5960 PRINT
5970 IF D0<>0 THEN 5720
5980 GOTO 1990
5989 '
5990 '  ***** KLINGONS SHOOTING *****
5991 '
6000 IF K3<=0 THEN RETURN
6010 IF D0<>0 THEN PRINT "STARBASE SHIELDS PROTECT THE ENTERPRISE": RETURN
6040 FOR I = 1 TO 3
6050   IF K(I,3) <= 0 THEN 6200
6060   H = INT((K(I,3)/FND(1))*(2+RND(1)))
6070   S = S-H
6072   K(I,3) = K(I,3)/(3+RND(0))
6080   PRINT H;"UNIT HIT ON ENTERPRISE FROM SECTOR";K(I,1);",";K(I,2)
6090   IF S <= 0 THEN 6240
6100   PRINT "      <SHIELDS DOWN TO";S;"UNITS>":
6110   IF H<20 THEN 6200
6120   IF (RND(1)> .6) OR (H/S<=.02) THEN 6200
6140   R1=FNR(1)
6150   D(R1) = D(R1) - H/S - .5*RND(1)
6160   GOSUB 8790
6170   PRINT "DAMAGE CONTROL REPORTS '";G2$;" DAMAGED BY THE HIT'"
6200 NEXT I
6202 RETURN
6209 '
6210 '   ***** END OF GAME *****
6211 '
6220 PRINT "IT IS STARDATE";T
6230 GOTO 6270
6240 PRINT: PRINT "THE ENTERPRISE HAS BEEN DESTROYED.  THE FEDERATION ";
6250 PRINT "WILL BE CONQUERED"
6260 GOTO 6220
6270 PRINT "THERE WERE";K9;"KLINGON BATTLE CRUISERS LEFT AT"
6280 PRINT "THE END OF YOUR MISSION."
6290 PRINT: PRINT
6300 IF B9=0 THEN 6360
6310 PRINT "THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
6320 PRINT "FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
6330 INPUT "LET HIM STEP FORWARD AND ENTER 'AYE'"; A$
6340 IF A$="AYE" THEN 370
6360 STOP
6370 PRINT "CONGRATULATIONS CAPTAIN!  THE LAST KLINGON BATTLE CRUISER"
6380 PRINT "MENACING THE FEDERATION HAS BEEN DESTROYED."
6390 PRINT
6400 PRINT "YOUR EFFICIENCY RATING IS "1000*(K7/(T-T0))^2
6410 GOTO 6290
6419 '
6420 '   ***** SHORT RANGE SENSOR SCAN & STARTUP SUBROUTINE *****
6421 '
6430 FOR I = (S1-1) TO (S1+1)
6440   FOR J = (S2-1) TO (S2+1)
6450     IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN 6540
6490     A$= ">!<"
6500     Z1=I
6510     Z2=J
6520     GOSUB 8830
6530     IF Z3=1 THEN 6580
6540   NEXT J
6560 NEXT I
6570 D0=0
6572 GOTO 6650
6580 D0=1
6590 C$="DOCKED"
6600 E=E0
6610 P=P0
6620 PRINT "SHIELDS DROPPED FOR DOCKING PURPOSES"
6630 S=0
6640 GOTO 6720
6650 IF K3>0 THEN C$="*RED*": GOTO 6720
6660 C$="GREEN"
6670 IF E<E0*.1 THEN C$="YELLOW"
6720 IF D(2)>= 0 THEN 6760
6730 PRINT:PRINT "*** SHORT RANGE SENSORS ARE OUT ***": PRINT
6740 RETURN
6760 LOCATE 1,1
6770 O1$="---------------------------------": PRINT O1$; : PRINT SPACE$(40)
6780 FOR I=1 TO 8
6790   FOR J = (I-1)*24+1 TO (I-1)*24+22 STEP 3
6800     IF MID$(Q$, J, 3) = "+K+" THEN COLOR 2
6802     IF MID$(Q$, J, 3) = "<*>" THEN COLOR 5
6806     IF MID$(Q$, J, 3) = ">!<" THEN COLOR 4
6810     PRINT " "; MID$(Q$, J, 3);
6815     COLOR 7,0
6820   NEXT J
6830   ON I GOTO 6850, 6900, 6960, 7020, 7070, 7120, 7180, 7240
6850 PRINT "        STARDATE          ";INT(T*10)*.1; SPACE$(10)
6860 GOTO 7260
6900 PRINT "        CONDITION          ";
6910 IF C$ = "GREEN" THEN COLOR 4 : GOTO 6940
6920 IF C$ = "*RED*" THEN COLOR 16,2 : GOTO 6940
6925 IF C$ = "DOCKED" THEN COLOR 8,4
6930 COLOR 16,6
6940 PRINT C$;
6950 COLOR 7,0 : PRINT SPACE$(10) : GOTO 7260
6960 PRINT "        QUADRANT          ";Q1;",";Q2; SPACE$(10)
6970 GOTO 7260
7020 PRINT "        SECTOR            ";S1;",";S2; SPACE$(10)
7030 GOTO 7260
7070 PRINT "        PHOTON TORPEDOES  ";INT(P); SPACE$(10)
7080 GOTO 7260
7120 PRINT "        TOTAL ENERGY      ";INT(E+S); SPACE$(10)
7130 GOTO 7260
7180 PRINT "        SHIELDS           ";INT(S); SPACE$(10)
7190 GOTO 7260
7240 PRINT "        KLINGONS REMAINING";INT(K9); SPACE$(10)
7260 NEXT I
7270 PRINT O1$
7272 RETURN
7279 '
7280 '   ***** LIBRARY COMPUTER CODE *****
7281 '
7290 IF D(8) < 0 THEN PRINT "COMPUTER DISABLED": GOTO 1990
7320 INPUT "COMPUTER ACTIVE AND AWAITING COMMAND";A
7330 IF A<0 THEN 1990
7350 PRINT
7352 H8=1
7354 ON A+1 GOTO 7540, 7900, 8070, 8500, 8150, 7400
7360 PRINT "FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
7370 PRINT "   0 = CUMULATIVE GALACTIC RECORD"
7372 PRINT "   1 = STATUS REPORT"
7374 PRINT "   2 = PHOTON TORPEDO DATA"
7376 PRINT "   3 = STARBASE NAV DATA"
7378 PRINT "   4 = DIRECTION/DISTANCE CALCULATOR"
7380 PRINT "   5 = GALAXY 'REGION NAME' MAP"
7382 PRINT
7384 GOTO 7320
7389 '
7390 '   ***** SETUP TO CHANGE CUM GAL RECORD TO GALAXY MAP *****
7391 '
7400 H8=0
7410 G5=1
7420 PRINT "                        THE GALAXY"
7430 GOTO 7550
7529 '   *********************************
7530 '   *****  CUM GALACTIC RECORD  *****
7531 '   *********************************
7540 INPUT "DO YOU WANT A HARDCOPY?  IS THE TTY ON (Y/N)"; A$
7542 IF A$="Y" THEN PRINT "HARDCOPY NOT PROGRAMMED"
7543 IF A$ <> "Y" THEN CLS
7544 PRINT "COMPUTER RECORD OF GALAXY FOR QUADRANT";Q1;",";Q2
7546 PRINT
7550 PRINT "       1     2     3     4     5     6     7     8"
7560 O1$ = "     ----- ----- ----- ----- ----- ----- ----- -----"
7570 PRINT O1$
7580 FOR I= 1 TO 8
7590   PRINT I;
7600   IF H8=0 THEN 7740
7630   FOR J = 1 TO 8
7640     PRINT "   ";
7650     IF Z(I,J)=0 THEN PRINT "***";: GOTO 7720
7660     QD$ = RIGHT$(STR$(Z(I,J) + 1000), 3)
7680     IF LEFT$(QD$,1) <> "0" THEN COLOR 2
7685     PRINT LEFT$(QD$,1); : COLOR 7
7690     IF MID$(QD$,2,1) <> "0" THEN COLOR 4
7695     PRINT MID$(QD$,2,1); : COLOR 7
7700     PRINT RIGHT$(QD$,1);
7720   NEXT J
7730   GOTO 7850
7740   Z4=I
7750   Z5=1
7760   GOSUB 9030
7770   J0=INT(15-.5*LEN(G2$))
7780   PRINT TAB(J0); G2$;
7800   Z5=5
7810   GOSUB 9030
7820   J0 = INT(39-.5*LEN(G2$))
7830   PRINT TAB(J0); G2$;
7850   PRINT
7860   PRINT O1$
7870 NEXT I
7880 PRINT
7884 PRINT "HIT ANY KEY WHEN READY TO CONTINUE"
7886 IF INKEY$ = "" THEN 7886
7888 CLS : GOTO 1990
7889 '   ***************************
7890 '   *****  STATUS REPORT  *****
7891 '   ***************************
7900 PRINT "   STATUS REPORT:"
7910 X$ = ""
7920 IF K9>1 THEN X$="S"
7940 PRINT "KLINGON"; X$; " LEFT: "; K9
7960 PRINT "MISSION MUST BE COMPLETED IN"; .1*INT((T0+T9-T)*10); "STARDATES"
7970 X$ = "S"
7972 IF B9<2 THEN X$=""
7974 IF B9<1 THEN 8010
7980 PRINT "THE FEDERATION IS MAINTAINING"; B9; "STARBASE";X$;" IN THE GALAXY"
7990 GOTO 5690
8010 PRINT "YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN IN"
8020 PRINT "  THE GALAXY -- YOU HAVE NO STARBASES LEFT!"
8030 GOTO 5690
8059 '
8060 '   ***** TORPEDO, BASE NAV, D/D CALCULATOR *****
8061 '
8070 IF K3<=0 THEN 4270
8080 X$=""
8082 IF K3 > 1 THEN X$="S"
8090 PRINT "FROM ENTERPRISE TO KLINGON BATTLE CRUSER";X$
8100 H8=0
8102 FOR I = 1 TO 3
8104   IF K(I,3) <= 0 THEN 8480
8110   W1 = K(I,1)
8112   X  = K(I,2)
8120   C1 = S1
8130   A  = S2
8140   GOTO 8220
8150   PRINT "DIRECTION/DISTANCE CALCULATOR:"
8160   PRINT "YOU ARE AT QUADRANT ";Q1;",";Q2;" SECTOR ";S1;",";S2
8170   PRINT "PLEASE ENTER"
8180   INPUT "  INITIAL COORDINATES (X,Y)";C1,A
8200   INPUT "  FINAL COORDINATES (X,Y)";W1,X
8220   X = X-A
8230   A = C1-W1
8240   IF X<0 THEN 8350
8250   IF A<0 THEN 8410
8260   IF X>0 THEN 8280
8270   IF A=0 THEN C1=5: GOTO 8290
8280   C1=1
8290   IF ABS(A) <= ABS(X) THEN 8330
8310   PRINT "DIRECTION =" C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A))
8320   GOTO 8460
8330   PRINT "DIRECTION =" C1+(ABS(A)/ABS(X))
8340   GOTO 8460
8350   IF A>0 THEN C1=3: GOTO 8290
8360   IF X<>0 THEN C1=5: GOTO 8290
8410   C1=7
8420   IF ABS(A) >= ABS(X) THEN 8450
8430   PRINT "DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
8440   GOTO 8460
8450   PRINT "DIRECTION =";C1+(ABS(X)/ABS(A))
8460   PRINT "DISTANCE =";SQR(X^2+A^2)
8470   IF H8=1 THEN 1990
8480 NEXT I
8490 GOTO 1990
8500 IF B3<>0 THEN PRINT "FROM ENTERPRISE TO STARBASE:"
8502 W1=B4
8504 X=B5
8506 GOTO 8120
8510 PRINT "MR. SPOCK REPORTS,  'SENSORS SHOW NO STARBASES IN THIS QUADRANT."
8520 GOTO 1990
8579 '
8580 '   ***** FIND EMPTY PLACE IN QUADRANT (FOR THINGS) *****
8581 '
8590 R1=FNR(1)
8592 R2=FNR(1)
8594 A$="   "
8596 Z1=R1
8597 Z2=R2
8598 GOSUB 8830
8599 IF Z3=0 THEN 8590
8600 RETURN
8659 '   ***********************************************
8660 '   ***** INSERT IN STRING ARRAY FOR QUADRANT *****
8661 '   ***********************************************
8670 S8 = INT(Z2-.5)*3 + INT(Z1-.5)*24 + 1
8675 IF LEN(A$)<>3 THEN PRINT "ERROR": STOP
8680 IF S8=1 THEN Q$ = A$+RIGHT$(Q$,189): RETURN
8690 IF S8=190 THEN Q$ = LEFT$(Q$,189)+A$: RETURN
8700 Q$ = LEFT$(Q$,S8-1) + A$ + RIGHT$(Q$,190-S8)
8710 RETURN
8779 '   ******************************
8780 '   ***** PRINTS DEVICE NAME *****
8781 '   ******************************
8790 ON R1 GOTO 8792, 8794, 8796, 8798, 8800, 8802, 8804, 8806
8792 G2$ = "WARP ENGINES" : RETURN
8794 G2$ = "SHORT RANGE SENSORS" : RETURN
8796 G2$ = "LONG RANGE SENSORS" : RETURN
8798 G2$ = "PHASER CONTROL" : RETURN
8800 G2$ = "PHOTON TUBES" : RETURN
8802 G2$ = "DAMAGE CONTROL" : RETURN
8804 G2$ = "SHIELD CONTROL" : RETURN
8806 G2$ = "LIBRARY-COMPUTER" : RETURN
8819 '   ***********************************************
8820 '   ***** STRING COMPARISON IN QUADRANT ARRAY *****
8821 '   ***********************************************
8830 Z1 = INT(Z1 + .5) : Z2 = INT(Z2 + .5) : S8 = (Z2 - 1)*3 + (Z1 - 1)*24 + 1
8835 Z3 = 0
8890 IF MID$(Q$, S8, 3) <> A$ THEN RETURN
8900 Z3 = 1 : RETURN
9009 '   ******************************************************
9010 '   ***** QUADRANT NAME IN G2$ FROM Z4, Z5 (=Q4, Q5) *****
9011 '   ******************************************************
9020 '   CALL WITH G5 = 1 TO GET REGION NAME ONLY
9030 IF Z5 <= 4 THEN ON Z4 GOTO 9040, 9050, 9060, 9070, 9080, 9090, 9100, 9110
9035 GOTO 9120
9040 G2$ = "ANTARES" : GOTO 9220
9050 G2$ = "RIGEL" : GOTO 9220
9060 G2$ = "PROCYON" : GOTO 9220
9070 G2$ = "VEGA" : GOTO 9220
9080 G2$ = "CANOPUS" : GOTO 9220
9090 G2$ = "ALTAIR" : GOTO 9220
9100 G2$ = "SAGITTARIUS" : GOTO 9220
9110 G2$ = "POLLUX" : GOTO 9220
9120 ON Z4 GOTO 9130, 9140, 9150, 9160, 9170, 9180, 9190, 9200
9130 G2$ = "SIRIUS" : GOTO 9210
9140 G2$ = "DENEB" : GOTO 9210
9150 G2$ = "CAPELLA" : GOTO 9210
9160 G2$ = "BETELGEUSE" : GOTO 9210
9170 G2$ = "ALDEBERAN" : GOTO 9210
9180 G2$ = "REGULUS" : GOTO 9210
9190 G2$ = "ARCTURUS" : GOTO 9210
9200 G2$ = "SPICA"
9210 IF G5 <> 1 THEN ON Z5 GOTO 9230, 9240, 9250, 9260, 9230, 9240, 9250, 9260
9220 RETURN
9230 G2$ = G2$ + " I" : RETURN
9240 G2$ = G2$ + " II" : RETURN
9250 G2$ = G2$ + " III" : RETURN
9260 G2$ = G2$ + " IV" : RETURN
9999 END
